if (isIndependent.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        multiDexEnabled true
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }

    sourceSets {
        main {
            // 在独立运行或者作为Library调试时，使用不同的AndroidManifest.xml文件
            if (isIndependent.toBoolean()) {
                manifest.srcFile 'src/demo/AndroidManifest.xml'

                java.srcDirs += ['src/demo/java']
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation project(':base')

    if (isIndependent.toBoolean()) {
        debugImplementation 'com.didichuxing.doraemonkit:dokitx:3.3.5'
        releaseImplementation 'com.didichuxing.doraemonkit:dokitx-no-op:3.3.5'
        implementation "org.jetbrains.kotlin:kotlin-reflect:1.4.10"
    }
}